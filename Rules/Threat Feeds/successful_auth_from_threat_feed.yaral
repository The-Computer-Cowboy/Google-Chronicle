rule successful_auth_from_threat_feed
{
  meta:
     author = "The-Computer-Cowboy"
     subject = "threat feed IP match detected inbound"
     description = "This rule is designed to identify a successful authentication from an IP address that have been marked as suspicious by threat intelligence source. Adversaries often utilize such connections as a vector for launching malicious activities including unauthorized access. "
     tactic = "Initial Access"
     technique = "Unauthorized Access"
     datasource = "Network Traffic"
     logsource = "NGFW, Firewall, Web proxy, Netflow, Network Security Monitor(NSM), Okta"
     confidence = "High"
     severity = "9"
     falsePositives = "It could be benign when threat intelligence source misclassifies threat association with IP address"
     version = "1"

  events:
        ($e1.metadata.event_type = "NETWORK_HTTP" or $e1.metadata.event_type = "NETWORK_CONNECTION")
        $e1.principal.ip != ""
        $e1.principal.ip = $principal_ip //Capturing source IP from event to correlate with Threat Intelligence feeds
        $e1.principal.ip_geo_artifact.location.country_or_region != ""
        not $e1.principal.ip in CIDR %whitelisted_threat_IP_feeds //to whitelist any threat feeds which are misclassfied as threat
        $e1.security_result.action = "ALLOW" //Looking for events which are allowed at security device
    
        $e2.graph.metadata.vendor_name = "<YOUR THREAT FEED HERE>" nocase
        $e2.graph.metadata.entity_type = "IP_ADDRESS"
        $e2.graph.metadata.threat.threat_feed_name in %threat_feed_names //looking at the reference list
        $e2.graph.entity.ip = $principal_ip
        //$e2.graph.metadata.threat.category_details != ""

        //Looking to see if the IP address from the threat feed is part of a user login in Okta
        $e3.metadata.log_type = "OKTA"
        $e3.metadata.event_type = "USER_LOGIN"
        $e3.principal.ip != ""
        $e3.principal.ip = $principal_ip

  match:
      $principal_ip over 5m

  condition:
    $e1 and $e2 and $e3
}
